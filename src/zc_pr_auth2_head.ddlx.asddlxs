@Metadata.layer: #CORE

@UI: { headerInfo: { typeName:       'Price Authorization',
                     typeNamePlural: 'Price Authorization',
                     title:          { type: #STANDARD,
                                       value: 'PriceAuth',
                                       label: 'Price Authorization' },
                     description:     { type: #STANDARD,
                                       value: 'Status' } } }

@UI.selectionVariant: [
  {
    text: 'Query',
    qualifier: 'Query'
  },
  {
    text: 'Draft',
    qualifier: 'Draft',
    filter: 'Status EQ "Draft"'
  },
  {
    text: 'Category Manager',
    qualifier: 'CategoryManager',
    filter: 'Status EQ "Submitted to Category Manager for Approval"'
  },
  {
    text: 'Accounting',
    qualifier: 'Accounting',
    filter: 'Status EQ "Approved by Category Manager"'
  },
  {
    text: 'Posted',
    qualifier: 'Posted',
    filter: 'Status EQ "Posted"'
  }
]

@UI.presentationVariant: [
  {
    text: 'Query',
    qualifier: 'Query',
    visualizations: [{type: #AS_LINEITEM, qualifier: 'Query'}]
  },
  {
    text: 'Draft',
    qualifier: 'Draft',
    visualizations: [{type: #AS_LINEITEM, qualifier: 'Draft'}]
  },
  {
    text: 'Category Manager',
    qualifier: 'CategoryManager',
    visualizations: [{type: #AS_LINEITEM, qualifier: 'CategoryManager'}]
  },
  {
    text: 'Accounting',
    qualifier: 'Accounting',
    visualizations: [{type: #AS_LINEITEM, qualifier: 'Accounting'}]
  },
  {
    text: 'Posted',
    qualifier: 'Posted',
    visualizations: [{type: #AS_LINEITEM, qualifier: 'Posted'}]
  }
]

@UI.selectionPresentationVariant: [
  {
    id: 'Query',
    text: 'Query',
    presentationVariantQualifier: 'Query',
    selectionVariantQualifier: 'Query',
    qualifier: 'Query'
  },
  {
    id: 'Draft',
    text: 'Draft',
    presentationVariantQualifier: 'Draft',
    selectionVariantQualifier: 'Draft',
    qualifier: 'Draft'
  },
  {
    id: 'CategoryManager',
    text: 'Category Manager',
    presentationVariantQualifier: 'CategoryManager',
    selectionVariantQualifier: 'CategoryManager',
    qualifier: 'CategoryManager'
  },
  {
    id: 'Accounting',
    text: 'Accounting',
    presentationVariantQualifier: 'Accounting',
    selectionVariantQualifier: 'Accounting',
    qualifier: 'Accounting'
  },
  {
    id: 'Posted',
    text: 'Posted',
    presentationVariantQualifier: 'Posted',
    selectionVariantQualifier: 'Posted',
    qualifier: 'Posted'
  }
]

annotate entity ZC_PR_AUTH2_HEAD with
{

  @UI.facet: [

    //Header

    //Parent
    {
      purpose: #HEADER,
      id: 'FacetCollection',
      type: #COLLECTION
    },

    //Fieldgroup in header section
    {
      parentId   : 'FacetCollection',
      label      : 'Record statistics:',
      type       : #FIELDGROUP_REFERENCE,
      targetQualifier: 'HeaderData'
    },

    //Datapoint for number of materials
    {
      purpose: #HEADER,
      position   : 10,
      type: #DATAPOINT_REFERENCE,
      targetQualifier: 'datapoint_material'
    },

    //Datapoint for number of impacted goods
    {
      purpose: #HEADER,
      position   : 20,
      type: #DATAPOINT_REFERENCE,
      targetQualifier: 'datapoint_impactedfg'
    },

     //Datapoint for number of notes
    {
      purpose: #HEADER,
      position   : 30,
      type: #DATAPOINT_REFERENCE,
      targetQualifier: 'datapoint_notes'
    },

     //Datapoint for number of messages
    {
      purpose: #HEADER,
      position   : 40,
      type: #DATAPOINT_REFERENCE,
      targetQualifier: 'datapoint_messages'
    },

       //Content
       {
         purpose: #STANDARD,
         id: 'Header',
         label: 'Request',
         position   : 10,
         type: #COLLECTION
       },
       {
         parentId   : 'Header',
         label      : 'General Data',
         type       : #FIELDGROUP_REFERENCE,
         position   : 10,
         targetQualifier: 'GeneralData'
       },
       {
         parentId   : 'Header',
         label      : 'Validity',
         type       : #FIELDGROUP_REFERENCE,
         position   : 20,
         targetQualifier: 'Validity'
       },
       {
         parentId   : 'Header',
         label      : 'Sales Area',
         type       : #FIELDGROUP_REFERENCE,
         position   : 30,
         targetQualifier: 'SalesArea'
       },
       {
         parentId   : 'Header',
         label      : 'File Processing',
         type       : #FIELDGROUP_REFERENCE,
         position   : 40,
         targetQualifier: 'FileProcessing'
       },
         {
             id         : 'Material',
             purpose    : #STANDARD,
             position   : 20,
             label      : 'Materials',
             type       :  #LINEITEM_REFERENCE,
             targetElement: '_Items'
         },
         {
             id         : 'ImpactedGoods',
             purpose    : #STANDARD,
             position   : 30,
             label      : 'Impacted Finished Goods',
             type       :  #LINEITEM_REFERENCE,
             targetElement: '_ImpactedGoods'
         },
         {
             id         : 'Messages',
             purpose    : #STANDARD,
             position   : 40,
             label      : 'Messages',
             type       :  #LINEITEM_REFERENCE,
             targetElement: '_Messages'
         },
         {
             id         : 'Notes',
             purpose    : #STANDARD,
             position   : 40,
             label      : 'Notes',
             type       :  #LINEITEM_REFERENCE,
             targetElement: '_Notes'
         }
       ]


  @UI.lineItem: [{ position: 10, qualifier: 'Query' }, 
                 { position: 10, qualifier: 'Draft' }, 
                 { position: 10, qualifier: 'CategoryManager' },
                 { position: 10, qualifier: 'Accounting' },
                 { position: 10, qualifier: 'Posted' }]
  @UI.selectionField: [{ position: 10 }]
  PriceAuth;

  @UI.selectionField: [{ element: '_Items.Material', position : 30 }]
  _Items;

  @UI.lineItem: [{ position: 20, qualifier: 'Query' }, 
                 { position: 20, qualifier: 'Draft' }, 
                 { position: 20, qualifier: 'CategoryManager' },
                 { position: 20, qualifier: 'Accounting' },
                 { position: 20, qualifier: 'Posted' }]
  @UI.selectionField: [{ position: 20 }]
  @UI.identification: [ { type: #FOR_ACTION, label: 'Import File', dataAction: 'importFile' },
                        { type: #FOR_ACTION, label: 'Export Data', dataAction: 'exportFile' },
                        { type: #FOR_ACTION, label: 'Validate',  dataAction: 'validateEntries' }]
  @UI.fieldGroup: [{ position: 10, qualifier: 'GeneralData' }]
  Description;

  @UI.fieldGroup: [{ position: 10, qualifier: 'Validity'}]
  ValidFrom;

  @UI.fieldGroup: [{ position: 20, qualifier: 'Validity'}]
  ValidTo;

  @UI.fieldGroup: [{ position: 11, qualifier: 'SalesArea'}]
  Salesorg;

  @UI.fieldGroup: [{ position: 12, qualifier: 'SalesArea'}]
  Distchannel;

  @UI.fieldGroup: [{ position: 13, qualifier: 'SalesArea'}]
  Division;

  @UI.lineItem: [{ position: 30, qualifier: 'Query' }, 
                 { position: 30, qualifier: 'Draft' }, 
                 { position: 30, qualifier: 'CategoryManager' },
                 { position: 30, qualifier: 'Accounting' },
                 { position: 30, qualifier: 'Posted' }]
  Localcreatedat;

  @UI.lineItem: [{ position: 40, qualifier: 'Query' }, 
                 { position: 40, qualifier: 'Draft' }, 
                 { position: 40, qualifier: 'CategoryManager' },
                 { position: 40, qualifier: 'Accounting' },
                 { position: 40, qualifier: 'Posted' }]
  Localcreatedby;

  @UI.lineItem: [{ position: 50, qualifier: 'Query' }, 
                 { position: 50, qualifier: 'Draft' }, 
                 { position: 50, qualifier: 'CategoryManager' },
                 { position: 50, qualifier: 'Accounting' },
                 { position: 50, qualifier: 'Posted' }]
//  @UI.fieldGroup: [{ position: 30, qualifier: 'GeneralData'}]
  Status;
  
  @UI.fieldGroup: [{ position: 31, qualifier: 'GeneralData', label: 'Choose an action'}]
  Action;

  @UI.lineItem: [{ hidden: true, qualifier: 'Query' }, 
                 { hidden: true, qualifier: 'Draft' }, 
                 { hidden: true, qualifier: 'CategoryManager' },
                 { hidden: true, qualifier: 'Accounting' },
                 { hidden: true, qualifier: 'Posted' }]
  @UI.selectionField: [{exclude: true}]
  @UI.fieldGroup: [{ position: 10, qualifier: 'HeaderData'}]
  @UI.dataPoint: {
      qualifier: 'datapoint_material',
      title: 'Materials'
  }
  Materialcount;

  @UI.lineItem: [{ hidden: true, qualifier: 'Query' }, 
                 { hidden: true, qualifier: 'Draft' }, 
                 { hidden: true, qualifier: 'CategoryManager' },
                 { hidden: true, qualifier: 'Accounting' },
                 { hidden: true, qualifier: 'Posted' }]
  @UI.selectionField: [{exclude: true}]
  @UI.fieldGroup: [{ position: 20, qualifier: 'HeaderData'}]
  @UI.dataPoint: {
      qualifier: 'datapoint_impactedfg',
      title: 'Impacted Finished Goods'
  }
  Impactedfgcount;

  @UI.lineItem: [{ hidden: true, qualifier: 'Query' }, 
                 { hidden: true, qualifier: 'Draft' }, 
                 { hidden: true, qualifier: 'CategoryManager' },
                 { hidden: true, qualifier: 'Accounting' },
                 { hidden: true, qualifier: 'Posted' }]
  @UI.selectionField: [{exclude: true}]
  @UI.fieldGroup: [{ position: 30, qualifier: 'HeaderData'}]
  @UI.dataPoint: {
      qualifier: 'datapoint_notes',
      title: 'Notes'
  }
  Notescount;

  @UI.lineItem: [{ hidden: true, qualifier: 'Query' }, 
                 { hidden: true, qualifier: 'Draft' }, 
                 { hidden: true, qualifier: 'CategoryManager' },
                 { hidden: true, qualifier: 'Accounting' },
                 { hidden: true, qualifier: 'Posted' }]
  @UI.selectionField: [{exclude: true}]
  @UI.fieldGroup: [{ position: 40, qualifier: 'HeaderData'}]
  @UI.dataPoint: {
      qualifier: 'datapoint_messages',
      title: 'Messages'
  }
  Messagescount;

  @UI.lineItem: [{ position: 60, qualifier: 'Query', importance: #HIGH }, 
                 { position: 60, qualifier: 'Draft', importance: #HIGH }, 
                 { position: 60, qualifier: 'CategoryManager', importance: #HIGH }, 
                 { position: 60, qualifier: 'Accounting', importance: #HIGH }, 
                 { position: 60, qualifier: 'Posted', importance: #HIGH }]
  @UI.fieldGroup: [{ position: 20, qualifier: 'GeneralData'}]
  SubmittedBy;
  
  @UI.fieldGroup: [{ position: 21, qualifier: 'GeneralData'}]
  SubmitTo;
  
  @UI.lineItem: [{ position: 70, qualifier: 'Query', importance: #HIGH }, 
                 { position: 70, qualifier: 'Draft', importance: #HIGH }, 
                 { position: 70, qualifier: 'CategoryManager', importance: #HIGH }, 
                 { position: 70, qualifier: 'Accounting', importance: #HIGH }, 
                 { position: 70, qualifier: 'Posted', importance: #HIGH }]
  SubmittedTo;

  @UI.fieldGroup: [{ position: 10, qualifier: 'FileProcessing'}]
  AttachmentUpload;

  @UI.fieldGroup: [{ position: 20, qualifier: 'FileProcessing'}]
  AttachmentDownload;

}